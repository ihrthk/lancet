apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'groovy'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile gradleApi()
    compileOnly "com.android.tools.build:gradle:$android_tools_version"
    compile "com.google.code.gson:gson:2.8.0"
    compile project(':lancet-weaver')
    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    testRuntime files(createClasspathManifest)
}

// 本地调试
def groupId = 'me.ele'
version = '1.0.8'
def artifactId = 'lancet-plugin'

// 上传本地maven库
uploadArchives {
    version = version + '-SNAPSHOT'
    repositories {
        mavenDeployer {
            snapshotRepository(url: uri('../snapshotRepo'))
            pom.groupId = groupId
            pom.artifactId = artifactId
            pom.version = version
        }
    }
}